plugins {
    id 'java-gradle-plugin'
    id 'checkstyle'
    id 'com.github.sherter.google-java-format' version '0.7.1'

    // For local install
    id 'maven'

    // Error-prone checker
    id 'net.ltgt.apt' version '0.18'
    id 'net.ltgt.errorprone' version '0.0.16'

    // Prepare release
    id 'net.researchgate.release' version '2.7.0'
    // Gradle Plugin Portal publish
    id 'com.gradle.plugin-publish' version '0.10.0'
}

group 'com.google.cloud.tools'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    // Use jcenter for gradle plugin portal releases.
    jcenter()
}

sourceSets {
    main {
        java.srcDir file('../skaffold-plugins-core/src/main/java')
        resources.srcDir file('../skaffold-plugins-core/src/main/resources')
    }
    test {
        java.srcDir file('../skaffold-plugins-core/src/test/java')
        resources.srcDir file('../skaffold-plugins-core/src/test/resources')
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
            srcDir file('../skaffold-plugins-core/src/integration-test/java')
        }
        resources {
            srcDir file('src/integration-test/resources')
            srcDir file('../skaffold-plugins-core/src/integration-test/resources')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    // These are copied over from skaffold-plugins-core and are necessary for the skaffold-plugins-core sourcesets.
    compile 'com.google.guava:guava:26.0-jre'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testCompile 'org.junit.jupiter:junit-jupiter-migrationsupport:5.2.0'
    testCompile 'org.mockito:mockito-core:2.21.0'

    compile gradleApi()

    // NullAway errorprone plugin
    annotationProcessor 'com.uber.nullaway:nullaway:0.5.2'
    errorprone 'com.google.errorprone:error_prone_core:2.3.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}

// Integration tests must be run explicitly
task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
integrationTest.dependsOn test

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

// Adds NullAway errorprone checks.
tasks.withType(JavaCompile) {
    if (!name.toLowerCase().contains('test')) {
        options.compilerArgs += ['-Xep:NullAway:ERROR', '-XepOpt:NullAway:AnnotatedPackages=com.google.cloud.tools']
    }
}

tasks.withType(Test) {
    reports.html.setDestination file("${reporting.baseDir}/${name}")
}

/* GOOGLE JAVA FORMAT */
googleJavaFormat {
    toolVersion = '1.6'
}
check.dependsOn verifyGoogleJavaFormat
/* GOOGLE JAVA FORMAT */

/* CHECKSTYLE */
checkstyle {
    toolVersion = '8.11'

    // get the google_checks.xml file from the checkstyle jar and take out the java checks
    def googleChecks = resources.text.fromArchiveEntry(configurations.checkstyle[0], 'google_checks.xml').asString()
    def fileExtensionsBefore = '<property name="fileExtensions" value="java, properties, xml"/>'
    def fileExtensionsAfter = '<property name="fileExtensions" value="properties, xml"/>'
    def googleChecksNoJava = googleChecks.replace(fileExtensionsBefore, fileExtensionsAfter)
    assert !googleChecks.equals(googleChecksNoJava)

    config = resources.text.fromString(googleChecksNoJava)

    maxErrors = 0
    maxWarnings = 0
}
/* CHECKSTYLE */

/* RELEASE */
// Prepare release
release {
    tagTemplate = 'v$version-gradle'
    git {
        requireBranch = /^gradle_release_v\d+.*$/  //regex
    }
}
// Gradle Plugin Portal releases
pluginBundle {
    website = 'https://github.com/GoogleContainerTools/skaffold-tools-for-java/'
    vcsUrl = 'https://github.com/GoogleContainerTools/skaffold-tools-for-java/'

    plugins {
        skaffoldPlugin {
            id = 'com.google.cloud.tools.skaffold'
            displayName = 'Skaffold tools for Java'
            description = 'Develop containerized Java applications on Kubernetes'
            tags = ['google', 'java', 'containers', 'docker', 'kubernetes', 'microservices']
        }
    }
}
tasks.publishPlugins.dependsOn integrationTest
/* RELEASE */
